unit Testjson;
{

  Delphi DUnit Test Case
  ----------------------
  This unit contains a skeleton test case class generated by the Test Case Wizard.
  Modify the generated code to correctly setup and call the methods from the unit 
  being tested.

}

interface

uses
  TestFramework, Variants, SysUtils, Classes, Dialogs, json, windows, dateutils;

type
  // Test methods for class TJSON

  TestTJSON = class(TTestCase)
  strict private
    function loadFile(const AFilename: string): string;
  public

  published
    procedure TestUser;
    procedure TestUserList;
    procedure TestListInListInList;
    procedure TestEmptyList;
  end;

var
  fmt: TFormatSettings;

implementation

function TestTJSON.loadFile(const AFilename: string): string;
var
  jsonFile: TextFile;
  text: string;
begin
  result := '';

  AssignFile(jsonFile, AFilename);
  try
    Reset(jsonFile);

    while not Eof(jsonFile) do
    begin
      ReadLn(jsonFile, text);
      result := result+text;
    end;
  finally
    CloseFile(jsonFile);
  end;
end;

procedure TestTJSON.TestEmptyList;
begin
  with TJSON.Parse(loadFile('test4.json')) do
  begin
    try
      check(IsList = false);
      check(assigned(_['empty'].ListItems) = true);
      check(_['empty'].ListItems.count = 0);
    finally
      Free;
    end;
  end;
  with TJSON.Parse(loadFile('test5.json')) do
  begin
    try
      check(IsList = true);
      check(assigned(ListItems) = true);
      check(ListItems.count = 0, inttostr(ListItems.count));
    finally
      Free;
    end;
  end;
end;

procedure TestTJSON.TestListInListInList;
begin
  with TJSON.Parse(loadFile('test3.json')) do
  begin
    try
      check(_[0].IsList = true);
      check(_[0][0][0].AsString = 'list in a list in a list');
    finally
      Free;
    end;
  end;
end;

procedure TestTJSON.TestUser;
var
  photo, item, item_a: TJSON;
  i: integer;
  fmtSettings: TFormatSettings;
begin
  GetLocaleFormatSettings(GetSystemDefaultLCID, fmtSettings);
  with TJSON.Parse(loadFile('test1.json')) do
  begin
    try
      Check(_['username'].AsString = 'thomas', _['username'].AsString);
      i := 0;
      for photo in _['photos'] do
      begin
        inc(i);
        check(photo['title'].AsString = format('Photo %d', [i]), 'title is not '+format('Photo %d', [i]));
        check(assigned(photo['urls']));
        check(photo['urls']['small'].AsString = format('http://example.com/photo%d_small.jpg', [i]), 'url is not '+format('http://example.com/photo%d_small.jpg', [i]));
        check(photo['urls']['large'].AsString = format('http://example.com/photo%d_large.jpg', [i]), 'url is not '+format('http://example.com/photo%d_large.jpg', [i]));
      end;
      i := 0;
      for item in _['int_list'] do
      begin
        inc(i);
        check(item.AsInteger = i);
      end;

      i := 0;
      for item in _['str_list'] do
      begin
        inc(i);
        check(item.AsString = inttostr(i));
      end;

      check(_['escape_text'].AsString = 'Some "test" \\ \u00e6=æ', format('%s is not Some "test" \\ \u00e6=æ', [_['escape_text'].AsString]));
      check(_['escape_path'].AsString = 'C:\test\test.txt', format('%s is not C:\test\test.txt', [_['escape_path'].AsString]));

      check(_['nullvalue'].AsString = '');
    finally
      Free;
    end;
  end;
end;

procedure TestTJSON.TestUserList();
var
  users: TJSON;
  user: TJSON;
  i: integer;

  u: TJSON;
begin
  users := TJSON.Parse(loadFile('test2.json'));
  try
    i := 0;
    check(users.ListItems.Count = 3, format('%d is not 3', [users.ListItems.Count]));
    for user in users do
    begin
      inc(i);
      case i of
        1: check(user['username'].AsString = 'thomas', user['username'].AsString+' is not thomas');
        2: check(user['name'].AsString = 'Kurt', user['name'].AsString+' is not kurt');
        3: check(user['username'].AsString = 'bent', user['username'].AsString+' is not bent');
      end;
    end;
  finally
    users.free;
  end;
end;

initialization
  // Register any test cases with the test runner
  RegisterTest(TestTJSON.Suite);
end.

